plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.14.0'
    id 'signing'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}


publishing {
    publications {
        maven(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            artifactId = 'auth-spring-boot-core'
            groupId = 'io.github.stefanodallapalma'
            version = '0.0.1'

            from components.java

            pom {
                name = 'Auth Spring Boot Core'
                description = 'Core module for the Auth Spring Boot project.'
                url = 'https://github.com/stefanodallapalma/auth-spring-boot'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'stefanodallapalma'
                        name = 'Stefano Dalla Palma'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/stefanodallapalma/auth-spring-boot.git'
                    developerConnection = 'scm:git:ssh://github.com/stefanodallapalma/auth-spring-boot.git'
                    url = 'http://github.com/stefanodallapalma/auth-spring-boot'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}


tasks.named('test') {
    useJUnitPlatform()
}