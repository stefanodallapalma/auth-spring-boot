plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jreleaser' version '1.14.0'
    id 'signing'
}

java {
    withJavadocJar()
    withSourcesJar()
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('target/staging-deploy')
                }
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.stefanodallapalma'
            artifactId = 'auth-spring-boot-starter'
            version = '0.0.1'

            from components.java

            pom {
                name = 'Auth Spring Boot Starter'
                description = 'A Spring Boot starter for authentication with JSON Web Tokens and Refresh Tokens'
                url = 'https://github.com/stefanodallapalma/auth-spring-boot'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'stefanodallapalma'
                        name = 'Stefano Dalla Palma'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/stefanodallapalma/auth-spring-boot.git'
                    developerConnection = 'scm:git:ssh://github.com/stefanodallapalma/auth-spring-boot.git'
                    url = 'http://github.com/stefanodallapalma/auth-spring-boot'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

signing {
//    useInMemoryPgpKeys(System.getenv("SIGNING_KEY"), System.getenv("SIGNING_PASSPHRASE"))
    useGpgCmd()
    sign publishing.publications.maven
}

jar {
    archiveBaseName = 'auth-spring-boot-starter'
    archiveVersion = '0.0.1'
    archiveClassifier = ''
    from(project(':web').sourceSets.main.output)
    from(project(':core').sourceSets.main.output)
}